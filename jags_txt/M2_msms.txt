
model {
  
  # Omega
  for (i in 1:ncells){
    logit(psi[i]) <- alpha.lpsi[region[i]] + beta.lpsi[1] * elev[i] + beta.lpsi[2] * elev[i]^2 + beta.lpsi[3] * temp[i] + beta.lpsi[4] * temp[i]^2 + beta.lpsi[5] * physdiv[i] + beta.lpsi[6] * precip[i] + beta.lpsi[7] * forest[i] + beta.lpsi[8] * wetlands[i]
    logit(r[i]) <- alpha.lr[region[i]] + beta.lr[1] * karst[i] + beta.lr[2] * forest[i] + beta.lr[3] * physdiv[i]
  }
  
  # Priors for parameters in the linear models of psi and r
  # Region-specific intercepts
  for (k in 1:3){
    alpha.lpsi[k] <- logit(mean.psi[k])
    mean.psi[k] ~ dunif(0, 1)
    alpha.lr[k] <- logit(mean.r[k])
    mean.r[k] ~ dunif(0, 1)
  }
  
  # Priors for coefficients of covariates in Omega
  for (k in 1:8){
    beta.lpsi[k] ~ dnorm(0, 0.1)
  }
  
  for (k in 1:3){
    beta.lr[k] ~ dnorm(0, 0.1)
  }
  
  # availability model with covariates
  for (i in 1:ncells){
    for (j in 1:nsites[i]){
      logit(theta2[i,j]) <- alpha.ltheta2 + beta.site.type.ltheta2[site_type[i,j]] +
        beta.ltheta2[1] * date[i,j] + beta.ltheta2[2] * date_sqrd[i,j] + 
        beta.ltheta2[3] * duration[i,j]
      
      mlogit.theta3[2,i,j] <- alpha.ltheta32 + beta.site.type.ltheta32[site_type[i,j]] +
        beta.ltheta32[1] * date[i,j] + beta.ltheta32[2] * date_sqrd[i,j] + 
        beta.ltheta32[3] * duration[i,j]
        
      mlogit.theta3[3,i,j] <- alpha.ltheta33 + beta.site.type.ltheta33[site_type[i,j]] +
        beta.ltheta33[1] * date[i,j] + beta.ltheta33[2] * date_sqrd[i,j] + 
        beta.ltheta33[3] * duration[i,j]
    }
  }
  
  # Priors for parameters in the linear models in Theta
  # intercepts --------
  alpha.ltheta2 <- logit(mean.alpha.theta2)
  mean.alpha.theta2 ~ dunif(0,1)
  alpha.ltheta32 ~ dnorm(0, 0.01)
  alpha.ltheta33 ~ dnorm(0, 0.01)
  
  # effects of site type -------
  beta.site.type.ltheta2[1] <- 0
  beta.site.type.ltheta32[1] <- 0
  beta.site.type.ltheta33[1] <- 0
  for (k in 2:8){
    beta.site.type.ltheta2[k] ~ dnorm(0, 0.1)
    beta.site.type.ltheta32[k] ~ dnorm(0, 0.1)
    beta.site.type.ltheta33[k] ~ dnorm(0, 0.1)
  }
  # coefficients of date and duration
  for (k in 1:3){
    beta.ltheta2[k] ~ dnorm(0, 0.1)
    beta.ltheta32[k] ~ dnorm(0, 0.1)
    beta.ltheta33[k] ~ dnorm(0, 0.1)
  }
  
  
  # Multinomial logit link for availability model for state 3 (= many bats)
  for (i in 1:ncells){
    for (j in 1:nsites[i]){
      theta3[2, i, j] <- exp(mlogit.theta3[2,i,j]) / (1 + exp(mlogit.theta3[2,i,j]) + exp(mlogit.theta3[3,i,j]))
      theta3[3, i, j] <- exp(mlogit.theta3[3,i,j]) / (1 + exp(mlogit.theta3[2,i,j]) + exp(mlogit.theta3[3,i,j]))
      theta3[1, i, j] <- 1 - theta3[2, i, j] - theta3[3, i, j]
    }
  }
  
  # Multinomial logit link for observation model for state 3
  p2 ~ dunif(0, 1)
  lp32 ~ dnorm(0, 0.001)
  lp33 ~ dnorm(0, 0.001)
  p32 <- exp(lp32) / (1 + exp(lp32) + exp(lp33))
  p33 <- exp(lp33) / (1 + exp(lp32) + exp(lp33))
  p31 <- 1-p32-p33                     
  
  # Define initial state vector (Omega)
  for (i in 1:ncells){
    Omega[i,1] <- 1 - psi[i]                 # Prob. of no bats
    Omega[i,2] <- psi[i] * (1-r[i])          # Prob. of occ. by a few bats
    Omega[i,3] <- psi[i] * r[i]              # Prob. of occ. by many bats
  }
  
  # Define availability matrix (Theta)
  # Order of indices: true state, observed state for each cell_site 
  for (i in 1:ncells){
    for (j in 1:nsites[i]){
        Theta[1,1,i,j] <- 1
        Theta[1,2,i,j] <- 0.0000001
        Theta[1,3,i,j] <- 0.0000001
        Theta[2,1,i,j] <- 1-theta2[i,j]
        Theta[2,2,i,j] <- theta2[i,j]
        Theta[2,3,i,j] <- 0.0000001
        Theta[3,1,i,j] <- theta3[1,i,j]                  
        Theta[3,2,i,j] <- theta3[2,i,j]
        Theta[3,3,i,j] <- theta3[3,i,j]
    }
  }

  
  # Define observation matrix (pDet)
  # Order of indices: true state, observed state
  pDet[1,1] <- 1
  pDet[1,2] <- 0.000001
  pDet[1,3] <- 0.000001
  pDet[2,1] <- 1-p2
  pDet[2,2] <- p2
  pDet[2,3] <- 0
  pDet[3,1] <- p31                    # = 1-p32-p33 as per prior section
  pDet[3,2] <- p32
  pDet[3,3] <- p33
  
  # State-space likelihood
  # State equation: model of true states (z)
  for (i in 1:ncells){
    z[i] ~ dcat(Omega[i,])
  }
  
  # Availability equation
  for (i in 1:ncells){
    for (j in 1:nsites[i]){
      a[i,j] ~ dcat(Theta[z[i],,i,j])
    }
  }
  
  # Observation equation
  for (i in 1:ncells){
    for (j in 1:nsites[i]){
      for (k in 1:nsurveys){
        y[i,j,k] ~ dcat(pDet[a[i,j],])
      }
    }
  }
  
  # Derived quantities
  for (i in 1:ncells){
    occ1[i] <- equals(z[i], 1)
    occ2[i] <- equals(z[i], 2)
    occ3[i] <- equals(z[i], 3)
  }
  n.occ[1] <- sum(occ1[]) # Grid cells in state 1
  n.occ[2] <- sum(occ2[]) # Grid cells in state 2
  n.occ[3] <- sum(occ3[]) # Grid cells in state 3
}
