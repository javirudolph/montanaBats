
model {

  ### (1) Priors
  ## Omega
  for (i in 1:ncells){
    logit(psi[i]) <- alpha.lpsi[region[i]] + beta.lpsi[1] * elev[i] + beta.lpsi[2] * elev[i]^2 + beta.lpsi[3] * temp[i] + beta.lpsi[4] * temp[i]^2 + beta.lpsi[5] * physdiv[i] + beta.lpsi[6] * precip[i] + beta.lpsi[7] * forest[i] + beta.lpsi[8] * wetlands[i]
    logit(r[i]) <- alpha.lr[region[i]] + beta.lr[1] * karst[i] + beta.lr[2] * forest[i] + beta.lr[3] * physdiv[i]
  }
  
  # Priors for parameters in the linear models of psi and r
  # Region-specific intercepts
  for (k in 1:nregion){
    alpha.lpsi[k] <- logit(mean.psi[k])
    mean.psi[k] ~ dunif(0, 1)
    alpha.lr[k] <- logit(mean.r[k])
    mean.r[k] ~ dunif(0, 1)
  }
  
  # Priors for coefficients of covariates in Omega
  for (k in 1:8){
    beta.lpsi[k] ~ dnorm(0, 0.1)
  }
  
  for (k in 1:3){
    beta.lr[k] ~ dnorm(0, 0.1)
  }
  
  ## Theta 
  # Priors for parameters in local occupancy
  theta1 ~ dunif(0, 1)
  theta2 ~ dunif(0, 1)
  theta3 ~ dunif(0, 1)
  
  ### (2) Define relationships between basic model structure and parameters
  # Define initial state vector
  for (i in 1:ncells){
    Omega[i,1] <- 1 - psi[i]                 # Prob. of no bats
    Omega[i,2] <- psi[i] * (1-r[i])          # Prob. of occ. by a few bats
    Omega[i,3] <- psi[i] * r[i]              # Prob. of occ. by many bats
  }
  
  # Define local occupancy probability matrix (Theta)
  # Order of indices: global state, local state
  Theta[1,1] <- 1
  Theta[1,2] <- 0
  Theta[1,3] <- 0
  Theta[2,1] <- 1-theta1
  Theta[2,2] <- theta1
  Theta[2,3] <- 0
  Theta[3,1] <- 1-theta2
  Theta[3,2] <- theta2 * (1-theta3)
  Theta[3,3] <- theta2 * theta3
  
  # Define the detection probability
  detP[1,1] <- 1
  detP[1,2] <- 0
  detP[1,3] <- 0
  detP[2,1] <- 0
  detP[2,2] <- 1
  detP[2,3] <- 0
  detP[3,1] <- 0
  detP[3,2] <- 0
  detP[3,3] <- 0
  
  ### (3) Likelihood
  

  for (i in 1:ncells){
    # Global occupancy
    z[i] ~ dcat(Omega[i,])
    for (j in 1:nsites){
      # Local occupancy
      u[i,j] ~ dcat(Theta[z[i], ])
      for (k in 1:nsurveys){
        # Detection on surveys
        y[i,j,k] ~ dcat(detP[u[i,j],])
      }
    }
  }
  
}
