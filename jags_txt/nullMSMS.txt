
model {
  ### (1) Priors for parameters
  # State process priors
  # Priors for parameters in initial state vector (Omega)
  psi ~ dunif(0, 1)
  r ~ dunif(0, 1)
  
  # Priors for parameters in local occupancy (Theta)
  for(s in 1:3){
    theta[s] ~ dunif(0, 1)
  }

  # Priors for parameters in observation process (varp)
  p2 ~ dunif(0, 1)                 # Detection prob. when in state 2
  for (s in 1:3) {                 # Detection prob. when in state 3
    beta[s] ~ dgamma(1, 1)         # Induce Dirichlet prior
    p3[s] <- beta[s] / sum(beta[])
  }
  ### (2) Define relationships between basic model structure and parameters
  # Define initial state vector: Year 1
  Omega[1] <- 1 - psi              # Prob. of non-occupation
  Omega[2] <- psi * (1-r)          # Prob. of occ. by a few bats
  Omega[3] <- psi * r              # Prob. of occ. by many bats
  
  # Define local occupancy probability (Theta)
  Theta[1,1] <- 1
  Theta[1,2] <- 0
  Theta[1,3] <- 0
  Theta[2,1] <- 1-theta[1]
  Theta[2,2] <- theta[1]
  Theta[2,3] <- 0
  Theta[3,1] <- 1-theta[2]
  Theta[3,2] <- theta[2] * (1 - theta[3])
  Theta[3,3] <- theta[2] * theta[3]
  
  # Define observation probability matrix (p)
  # Order of indices: true state, observed state
  varp[1,1] <- 1
  varp[1,2] <- 0.000001
  varp[1,3] <- 0.000001
  varp[2,1] <- 1-p2
  varp[2,2] <- p2
  varp[2,3] <- 0.000001
  varp[3,1] <- p3[1]
  varp[3,2] <- p3[2]
  varp[3,3] <- p3[3]
  
  ### (3) Likelihood
  # Initial state: year 1
  for (i in 1:ncells){
    z[i] ~ dcat(Omega[])
  }
  
  # # State transitions from yearly interval
  # for (i in 1:ncells){
  #   for (t in 2:nyears){
  #     z[i,t] ~ dcat(PhiMat[z[i,t-1],])
  #   }
  # }
  
  # local occupancy
  
  for (i in 1:ncells){
    for (j in 1:nsites){
        u[i,j] ~ dcat(Theta[z[i],])
      }
  }
  
  
  # Observation equation
    for (i in 1:ncells){
      for (j in 1:nsites){
        for (k in 1:nsurveys){
          y[i,j,k] ~ dcat(varp[u[i,j],])
        }
      }
    }
  
  ### (4) Derived quantities
  sum.z <- sum(z[])
}
