
model {


  # Priors
  
  # Omega
  for (i in 1:ncells){
    logit(psi[i]) <- alpha.lpsi[region[i]] + beta.lpsi[1] * elev[i] + beta.lpsi[2] * elev[i]^2 + beta.lpsi[3] * temp[i] + beta.lpsi[4] * temp[i]^2 + beta.lpsi[5] * physdiv[i] + beta.lpsi[6] * precip[i] + beta.lpsi[7] * forest[i] + beta.lpsi[8] * wetlands[i]
    logit(r[i]) <- alpha.lr[region[i]] + beta.lr[1] * karst[i] + beta.lr[2] * forest[i] + beta.lr[3] * physdiv[i]
  }
  
  # Priors for parameters in the linear models of psi and r
  # Region-specific intercepts
  for (k in 1:6){
    alpha.lpsi[k] <- logit(mean.psi[k])
    mean.psi[k] ~ dunif(0, 1)
    alpha.lr[k] <- logit(mean.r[k])
    mean.r[k] ~ dunif(0, 1)
  }
  
  # Priors for coefficients of covariates in Omega
  for (k in 1:8){
    beta.lpsi[k] ~ dnorm(0, 0.1)
  }
  
  for (k in 1:3){
    beta.lr[k] ~ dnorm(0, 0.1)
  }
  
  # Multinomial logit link for availability model for state 3 (= many bats)
  theta2 ~ dunif(0,1)
  ltheta32 ~ dnorm(0, 0.001)
  ltheta33 ~ dnorm(0, 0.001)
  theta32 <- exp(ltheta32) / (1 + exp(ltheta32) + exp(ltheta33))
  theta33 <- exp(ltheta33) / (1 + exp(ltheta32) + exp(ltheta33))
  theta31 <- 1-theta32-theta33  
  
  # Multinomial logit link for observation model for state 3
  p2 ~ dunif(0, 1)
  lp32 ~ dnorm(0, 0.001)
  lp33 ~ dnorm(0, 0.001)
  p32 <- exp(lp32) / (1 + exp(lp32) + exp(lp33))
  p33 <- exp(lp33) / (1 + exp(lp32) + exp(lp33))
  p31 <- 1-p32-p33                     
  
  # Define initial state vector (Omega)
  for (i in 1:ncells){
    Omega[i,1] <- 1 - psi[i]                 # Prob. of no bats
    Omega[i,2] <- psi[i] * (1-r[i])          # Prob. of occ. by a few bats
    Omega[i,3] <- psi[i] * r[i]              # Prob. of occ. by many bats
  }
  
  # Define availability matrix (Theta)
  # Order of indices: true state, observed state
  Theta[1,1] <- 1
  Theta[1,2] <- 0
  Theta[1,3] <- 0
  Theta[2,1] <- 1-theta2
  Theta[2,2] <- theta2
  Theta[2,3] <- 0
  Theta[3,1] <- theta31                    
  Theta[3,2] <- theta32
  Theta[3,3] <- theta33
  
  # Define observation matrix (pDet)
  # Order of indices: true state, observed state
  pDet[1,1] <- 1
  pDet[1,2] <- 0.000001
  pDet[1,3] <- 0.000001
  pDet[2,1] <- 1-p2
  pDet[2,2] <- p2
  pDet[2,3] <- 0
  pDet[3,1] <- p31                    # = 1-p32-p33 as per prior section
  pDet[3,2] <- p32
  pDet[3,3] <- p33
  
  # State-space likelihood
  # State equation: model of true states (z)
  for (i in 1:ncells){
    z[i] ~ dcat(Omega[i,])
  }
  
  # Availability equation
  for (i in 1:ncells){
    for (j in 1:nsites){
      a[i,j] ~ dcat(Theta[z[i],])
    }
  }
  
  # Observation equation
  for (i in 1:ncells){
    for (j in 1:nsites){
      for (k in 1:nsurveys){
        y[i,j,k] ~ dcat(pDet[a[i,j],])
      }
    }
  }
  
  # Derived quantities
  for (i in 1:ncells){
    occ1[i] <- equals(z[i], 1)
    occ2[i] <- equals(z[i], 2)
    occ3[i] <- equals(z[i], 3)
  }
  n.occ[1] <- sum(occ1[]) # Grid cells in state 1
  n.occ[2] <- sum(occ2[]) # Grid cells in state 2
  n.occ[3] <- sum(occ3[]) # Grid cells in state 3
}
