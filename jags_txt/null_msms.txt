
model {
  # Priors
  psi ~ dunif(0, 1)
  r ~ dunif(0, 1)
  theta2 ~ dunif(0,1)
  p2 ~ dunif(0, 1)
  
  # Multinomial logit link for availability model for state 3 (= many bats)
  ltheta32 ~ dnorm(0, 0.001)
  ltheta33 ~ dnorm(0, 0.001)
  theta32 <- exp(ltheta32) / (1 + exp(ltheta32) + exp(ltheta33))
  theta33 <- exp(ltheta33) / (1 + exp(ltheta32) + exp(ltheta33))
  theta31 <- 1-theta32-theta33  
  
  # Multinomial logit link for observation model for state 3
  lp32 ~ dnorm(0, 0.001)
  lp33 ~ dnorm(0, 0.001)
  p32 <- exp(lp32) / (1 + exp(lp32) + exp(lp33))
  p33 <- exp(lp33) / (1 + exp(lp32) + exp(lp33))
  p31 <- 1-p32-p33                     
  
  # Define initial state vector (Omega)
  Omega[1] <- 1 - psi                  # Prob. of no bats
  Omega[2] <- psi * (1-r)              # Prob. of occupancy (w/ few bats)
  Omega[3] <- psi * r                  # Prob. of occupancy (with many bats)
  
  # Define availability matrix (Theta)
  # Order of indices: true state, observed state
  Theta[1,1] <- 1
  Theta[1,2] <- 0
  Theta[1,3] <- 0
  Theta[2,1] <- 1-theta2
  Theta[2,2] <- theta2
  Theta[2,3] <- 0
  Theta[3,1] <- theta31                    
  Theta[3,2] <- theta32
  Theta[3,3] <- theta33
  
  # Define observation matrix (pDet)
  # Order of indices: true state, observed state
  pDet[1,1] <- 1
  pDet[1,2] <- 0
  pDet[1,3] <- 0
  pDet[2,1] <- 1-p2
  pDet[2,2] <- p2
  pDet[2,3] <- 0
  pDet[3,1] <- p31                    # = 1-p32-p33 as per prior section
  pDet[3,2] <- p32
  pDet[3,3] <- p33
  
  # State-space likelihood
  # State equation: model of true states (z)
  for (i in 1:ncells){
    z[i] ~ dcat(Omega[])
  }
  
  # Availability equation
  for (i in 1:ncells){
    for (j in 1:nsites){
      a[i,j] ~ dcat(Theta[z[i],])
    }
  }
  
  # Observation equation
  for (i in 1:ncells){
    for (j in 1:nsites){
      for (k in 1:nsurveys){
        y[i,j,k] ~ dcat(pDet[a[i,j],])
      }
    }
  }
  
  # Derived quantities
  for (i in 1:ncells){
    occ1[i] <- equals(z[i], 1)
    occ2[i] <- equals(z[i], 2)
    occ3[i] <- equals(z[i], 3)
  }
  n.occ[1] <- sum(occ1[]) # Grid cells in state 1
  n.occ[2] <- sum(occ2[]) # Grid cells in state 2
  n.occ[3] <- sum(occ3[]) # Grid cells in state 3
}
